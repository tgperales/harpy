// local.properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

// key.properties
boolean hasKeyStore = false

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    hasKeyStore = true
} else {
    println "+++"
    println "No keystore defined. The app will not be signed."
    println "Create a android/key.properties file with the following properties:"
    println "storePassword"
    println "keyPassword"
    println "keyAlias"
    println "storeFile"
    println "+++"
}

// version.properties
def versionProperties = new Properties()
def versionPropertiesFile = rootProject.file('version.properties')
if (versionPropertiesFile.exists()) {
    versionPropertiesFile.withReader('UTF-8') { reader ->
        versionProperties.load(reader)
    }
}

def freeVersionCode = versionProperties.getProperty('freeVersionCode')
def freeVersionName = versionProperties.getProperty('freeVersionName')

def proVersionCode = versionProperties.getProperty('proVersionCode')
def proVersionName = versionProperties.getProperty('proVersionName')

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 30

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        // 19+ required by the webview_flutter plugin
        minSdkVersion 19
        targetSdkVersion 30
        multiDexEnabled true
    }

    flavorDimensions "app"
    productFlavors {
        free {
            applicationId "com.robertodoering.harpy.free"
            versionCode freeVersionCode.toInteger()
            versionName freeVersionName
        }
        pro {
            applicationId "com.robertodoering.harpy.pro"
            versionCode proVersionCode.toInteger()
            versionName proVersionName
        }
    }

    if (hasKeyStore) {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            if (hasKeyStore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava' // fixes guava conflict: https://stackoverflow.com/questions/56639529/duplicate-class-com-google-common-util-concurrent-listenablefuture-found-in-modu
    implementation 'com.android.support:multidex:1.0.3' // multidex support for android <=21
}

if (hasKeyStore) {
    // enable google services for firebase analytics when signing the app
    apply plugin: 'com.google.gms.google-services'
}
